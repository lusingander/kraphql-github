plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
    id "com.github.lusingander.kraphql-plugin" version "0.0.1"
}

group 'com.github.lusingander'
version '0.0.7'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "kraphql-github"
                description = "Kotlin DSL for GitHub GraphQL API (GitHub API v4)"
                url = "https://github.com/lusingander/kraphql-github"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/lusingander/kraphql-github/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "lusingander"
                        name = "Kyosuke Fujimoto"
                        email = "kyoro.f@gmail.com"
                    }
                }
                scm {
                    url = "https://github.com/lusingander/kraphql-github"
                    connection = "scm:svn:https://github.com/lusingander/kraphql-github.git"
                    developerConnection = "scm:svn:https://github.com/lusingander/kraphql-github.git"
                }
            }
        }
    }
}

kraphql {
    input = "./schema.docs.graphql"
    output = "./src/main/kotlin/com/github/lusingander/kraphql/github/Dsl.kt"
    packageName = "com.github.lusingander.kraphql.github"
}
